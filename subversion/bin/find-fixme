#! /usr/bin/perl

# $Id: find-fixme 653 2007-06-26 15:36:49Z genehack $

use strict;
use warnings;

use Cwd;
use File::Basename;
use File::Find;
use File::Slurp;

my $progname = fileparse( $0 );

my $dir = getcwd;

if ( my $newdir = shift ) {
  if ( -d $newdir ) {
    $dir = $newdir;
    chdir $dir;
  }
  elsif ( -e $newdir ) {
    die "$progname: $newdir: not a dir\n";
  }
  else {
    die "$progname: $newdir: no such dir\n";
  }
}

$dir =~ s|/$|| if $dir =~ m|/$|;

find(\&wanted , $dir );

# wanted
sub wanted {
  # skip dotfiles
  return if /^\./;
  # skip emacs swap files
  return if /^\#.*\#$/;
  return if /~$/;
  # skip directories
  return if -d;
  # skip links
  return if -l;
  
  # skip everything in a .svn tree
  return if $File::Find::name =~ m|/\.svn/|;

  # if we got to this point, it's a file we're interested in scanning for
  # FIXME indicators, so generate a relative filename for later output
  my $masked_name = $File::Find::name;
  $masked_name =~ s/$dir/./;

  # then slurp in the file, look for matches, and print them out
  foreach my $line ( read_file( $_ )) {
    if ( $line =~ /### FIXME/ ) {
      # remove leading spaces, just to make things a bit more compact in the
      # output
      $line =~ s/^\s*//g;
      printf "%-15s -> %s" , $masked_name , $line;
    }
  }
} #/wanted
